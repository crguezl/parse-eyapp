
  # File 'calc.eyp': translates infix expressions to postfix
  # Compile it with:  eyapp -o calc.pl -C Postfix.eyp
  # Execution:        ./calc.pl -c 'a = 2*3+b'
  %token NUM = /([0-9]+(?:\.[0-9]+)?)/
  %token VAR = /([A-Za-z][A-Za-z0-9_]*)/

  %right  '='
  %left   '-' '+'
  %left   '*' '/'
  %left   NEG

  %defaultaction { "$left $right $op"; }

  %%
  line: $exp  { print "$exp\n" }
  ;

  exp:        $NUM  { $NUM }            
          |   $VAR  { $VAR }            
          |   VAR.left '='.op exp.right         
          |   exp.left '+'.op exp.right         
          |   exp.left '-'.op exp.right        
          |   exp.left '*'.op exp.right       
          |   exp.left '/'.op exp.right      
          |   '-' $exp %prec NEG { "$exp NEG" }
          |   '(' $exp ')' { $exp }      
  ;

  %%
